#!/usr/bin/osascript

(**
  * AppleTweet
  * @link http://code.google.com/p/appletweet/
  * @version 2.0.012 (March 9 2009)
  * @author Jason Aldridge
  * @copyright Copyright (C) 2009, Jason Aldridge
  * 
  * Description:
  * AppleTweet is an AppleScript wrapper for curl that invokes the Twitter API
  * and integrates with Perceptive Automation's Indigo Home Automation
  * software.
  *
  * Its primary aim is to supplement Indigo's reporting capability with logging
  * to Twitter in a semi-user-friendly manner.  Secondarily, AppleTweet provides 
  * other functions that have come up in the process of Twitter-Logging from an
  * Apple Mac Mini.
  *
  * To see an example of what can be done with Indigo and AppleTweet, please
  * visit the following sites: 
  * @link http://www.twitter.com/JCHouse Jason and Cheryll's House on Twitter
  * @link http://house.jasonandcheryll.com/ Journey to Skynet (Blog)
  *
  * The following functions are provided:
  * @see DoubleCheck()
  * @see Tweet()
  * @see TweetWithTime()
  *
  * ===============================LICENSE=====================================
  * This program is free software: you can redistribute it and/or modify it 
  * under the terms of the GNU General Public License as published by the Free 
  * Software Foundation, version 3 of the License.
  *
  * This program is distributed in the hope that it will be useful, but WITHOUT
  * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or 
  * FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for 
  * more details.
  *
  * You should have received a copy of the GNU General Public License
  * along with this program.  If not, see <http://www.gnu.org/licenses/>.
  * =============================END LICENSE===================================
*)

-- Pretty much everything we're going to do uses Indigo, so I invoke it at the
-- beginning.  It could also be invoked in each function as necessary, but meh.
using terms from application "IndigoServer"
    (**
        * DoubleCheck that a device is in the state that we expect it to be and 
        * send a specific Tweet if it is not.
        * @param string DeviceName Indigo name of the device to check status of.
        * @param string ExpectedStatus Expected status of the device.
        * @param string TweetIfUnmatched What to say if the state doesn't match.
        * @uses Tweet() to send the message.
    *)
    on DoubleCheck(DeviceName, ExpectedStatus, TweetIfUnmatched)
        set verbose to false
        tell application "IndigoServer"
            if (device DeviceName exists) then
                if (ExpectedStatus is "ON" or ExpectedStatus is "OFF") then
                    if (get supports on off of device DeviceName) then
                        if (ExpectedStatus is "ON") then
                            set binExpectedStatus to true
                        else
                            set binExpectedStatus to false
                        end if
                        if (binExpectedStatus is not equal to on state of device DeviceName) then
                            Tweet(TweetIfUnmatched)
                            log TweetIfUnmatched using type "UnexpectedResult"
                            return False
                        else
                            return True
                        end if
                    else
                        log DeviceName & " does not support the status: " & ExpectedStatus using type "DeviceError"
                        return False
                    end if
                end if
            else
                log DeviceName & " does not exist." using type "ScriptEror"
                return False
            end if
        end tell
    end DoubleCheck
    (**
        * DoubleCheck that a device is in the state that we expect it to be and 
        * send a specific Tweet if it is not.  Additionally, if the device is
        * not in the state we expect, set it to that state.
        * @param string DeviceName Indigo name of the device to check status of.
        * @param string ExpectedStatus Expected status of the device.
        * @param string TweetIfUnmatched What to say if the state doesn't match.
        * @param string TweetIfFixed What to say if the state doesn't match and 
        *               we fix it.
        * @uses DoubleCheck() to determine status.
    *)
    on DoubleCheckAndFix(DeviceName, ExpectedStatus, TweetIfUnmatched, TweetIfFixed)
        if(DoubleCheck(DeviceName,ExpectedStatus,TweetIfUnmatched) is False) then
            tell application "IndigoServer"
                if (ExpectedStatus is "ON") then
                    turn on DeviceName
                else if (ExpectedStatus is "OFF")
                    turn off DeviceName
                end if
            end tell
            if(DoubleCheck(DeviceName,ExpectedStatus,TweetIfUnmatched) is True) then
                Tweet(TweetIfFixed)
            end if
        end if
    end DoubleCheckAndFix
    on escapeshellarg(strShellArg)
        set escape_characters to "\"#&;`|*?~<>^()[]{}$\\/'!"
        set escaped_text to ""
        repeat with this_char in strShellArg
            if this_char is in escape_characters then
                set escaped_text to (escaped_text & "\\" & this_char) as string 
            else
                set escaped_text to (escaped_text & this_char) as string
            end if
        end repeat
        return escaped_text
    end escapeshellarg
    on AuthError(LogMessage)
        log "Could not authenticate to Twitter.  " & LogMessage using type "AuthError"
    end AuthError
    on MissingVariable(ProblemVariable,VariableContains)
            log "Could not log to Twitter.  Create an Indigo variable named " & ProblemVariable & " that contains " & VariableContains & "." using type "MissingVariable"
    end VariableError
    on Tweet(TwitterStatus)
        set verbose to false
        tell application "IndigoServer"
            if (TwitterStatus is null or TwitterStatus is equal to "") then
                log "Could not log to Twitter.  The Twitter Status was blank." using type "ScriptError"
            else
                set TwitterStatus to "status=" & TwitterStatus
                if (variable "TwitterAPIURL" exists) then
                    set TwitterAPIURL to value of variable "TwitterAPIURL"
                    if (variable "TwitterUsername" exists) then
                        set TwitterUsername to value of variable "TwitterUsername"
                        if (variable "TwitterPassword" exists) then
                            set TwitterPassword to value of variable "TwitterPassword"
                            set ShellScript to "curl --user \"" & TwitterUsername & ":" & TwitterPassword & "\" --data-binary \"source=jchousesappletweet&" & TwitterStatus & "\" \"" & TwitterAPIURL & "\""
                            if (verbose is equal to true) then
                                log "Twitter trying: " & ShellScript using type "VerboseOutput"
                            end if
                            set results to do shell script ShellScript
                            if (results contains "Could not authenticate") then
                                AuthError("Check your username and password.")
                            end if
                            if (results contains "Client must provide a 'status' parameter") then
                                log "Twitter did not accept the status: " & TwitterStatus using type "ParamError"
                            end if
                            if (verbose is equal to true) then
                                log "Twitter Status: " & results using type "VerboseOutput"
                            end if
                        else
                            MissingVariable("TwitterPassword","your Twitter password")
                        end if
                    else
                        MissingVariable("TwitterUserName","your Twitter username")
                    end if
                else
                    MissingVariable("TwitterAPIURL","the API URL for Twitter")
                end if
            end if
        end tell
    end Tweet
    (**
        * Modify the TwitterStatus to have the current timestamp at the end in
        * the format of ([H]H:MM AM/PM).  This is used to send duplicate status
        * updates since the Twitter REST API ignores a status update if it is 
        * the status has not changed.
        * @param string TwitterStatus The Twitter Status that should be set.
        * @uses Tweet() to send the message.
    *)
    on TweetWithTime(TwitterStatus)
        if (TwitterStatus is null or TwitterStatus is equal to "") then
            log "Could not log to Twitter.  The Twitter Status was blank." using type "ScriptError"
		else
            set TimeParts to words of (time string of (current date))
            set TimeWithoutSeconds to (item 1 of TimeParts) & ":" & (item 2 of TimeParts) & " " & (item 4 of TimeParts)
			Tweet(TwitterStatus & " (" & TimeWithoutSeconds & ")")
		end if
    end TweetTimeStamp
end using terms from